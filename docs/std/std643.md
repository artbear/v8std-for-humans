# Работа в разных часовых поясах

https://its.1c.ru/db/v8std#content:643

###### 1.

Сервер может работать в одном часовом поясе, пользователи в другом. Операции должны выполняться по местному времени пользователей.

Например: Сервер в Москве, пользователи во Владивостоке. Операции - по времени Владивостока.

###### 2.1.

Используйте `#!bsl ТекущаяДатаСеанса` вместо `#!bsl ТекущаяДата` в серверном контексте. `#!bsl ТекущаяДатаСеанса` приводит время к часовому поясу пользовательского сеанса. `#!bsl ТекущаяДата` возвращает дату в время серверного компьютера.

###### 2.2.

Когда требуется учесть время не зависящее от часового пояса текущего сеанса пользоватля используйте `#!bsl УниверсальноеВремя`.

Например: получить время последнего выполнения фонового задания или момента устаревания кеша.

###### 2.3.

Если метод платформы возвращает время в часовом поясе сервера, а время надо показать пользователю, приведите время к часовому поясу пользователя.

!!! Например

    ```bsl
    ДатаАктуальностиУниверсальная = УниверсальноеВремя(ПолнотекстовыйПоиск.ДатаАктуальности());
    ДатаАктуальности = МестноеВремя(ДатаАктуальностиУниверсальная, ЧасовойПоясСеанса());
    ```

###### 3.1.

Не используйте `#!bsl ТекущаяДата` на клиенте. Это может привести к сложнорасследуемому неожиданному поведению.

Попробуйте вместо этого:

- Передайте с сервера на клиент время и дату, приведенную к часовому поясу пользователя.
- При работе с документами на клиенте используйте дату документа.

###### 3.2.

!!! Например

    === "Неправильно"

        ```bsl title="Определять месяц, который надо закрыть, получая дату с клиента" hl_lines="6"
        &НаКлиенте
        Процедура КомандаОткрытьЗакрытиеМесяца(Команда)
            
            ТекущиеДанные = Элементы.Список.ТекущиеДанные;
            Если ТекущиеДанные = Неопределено Тогда
                ТекДата = ТекущаяДата();  // вызов ТекущаяДата() на клиенте
            Иначе
                ТекДата = ТекущиеДанные.Дата;
            КонецЕсли;
            ПараметрыФормы = Новый Структура;
            ПараметрыФормы.Вставить("ПериодРегистрации", ТекДата);
            ОткрытьФорму("Обработка.ЗакрытиеМесяца.Форма.Форма", ПараметрыФормы, ЭтотОбъект);
            
            ...
        ```
        ```bsl title="А при обработке использовать дату сервера" hl_lines="6"
        &НаСервере
        Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
            
            ЗаполнитьЗначенияСвойств(Объект, Параметры);
            Если Не ЗначениеЗаполнено(Объект.ПериодРегистрации) Тогда
                Объект.ПериодРегистрации = НачалоМесяца(ТекущаяДата());
            КонецЕсли;
        
            ...
        ```

    === "Правильно"

        ```bsl title="Определение даты переложить на сервер" hl_lines="6"
        &НаКлиенте
        Процедура КомандаОткрытьЗакрытиеМесяца(Команда)
            
            ТекущиеДанные = Элементы.Список.ТекущиеДанные;
            Если ТекущиеДанные = Неопределено Тогда
                ТекДата = Неопределено; // нет вызова ТекущаяДата() на клиенте
            Иначе
                ТекДата = ТекущиеДанные.Дата;
            КонецЕсли;
            ПараметрыФормы = Новый Структура;
            ПараметрыФормы.Вставить("ПериодРегистрации", ТекДата);
            ОткрытьФорму("Обработка.ЗакрытиеМесяца.Форма.Форма", ПараметрыФормы, ЭтотОбъект);

            ...
        ```
        ```bsl title="При обработке использвать ТекущаяДатаСеанса" hl_lines="6"
        &НаСервере
        Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
            
            ЗаполнитьЗначенияСвойств(Объект, Параметры);
            Если Не ЗначениеЗаполнено(Объект.ПериодРегистрации) Тогда
                Объект.ПериодРегистрации = НачалоМесяца(ТекущаяДатаСеанса());
            КонецЕсли;

            ...
        ```

###### 3.3.

При работе с документами использйте дату документа вместо текущей даты.

!!! Например

    === "Неправильно"

        ```bsl title="Не передавайте текущую дату для подбора номенклатуры в табличную часть" hl_lines="5-6"
        &НаКлиенте
        Процедура ПодборТовары(Команда)
        
            ПараметрыПодбора = Новый Структура;
            ДатаРасчетов = ?(НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДата()), Неопределено, Объект.Дата); // вызов ТекущаяДата() на клиенте
            ПараметрыПодбора.Вставить("ДатаРасчетов", ДатаРасчетов);
            ...
            ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора,
                ЭтотОбъект, УникальныйИдентификатор);

            ...
        ```
    === "Правильно"

        ```bsl title="Передавайте дату документа" hl_lines="5"
        &НаКлиенте
        Процедура ПодборТовары(Команда)
            
            ПараметрыПодбора = Новый Структура;
            ПараметрыПодбора.Вставить("ДатаРасчетов", Объект.Дата);
            
            ...
            ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора,
                ЭтотОбъект, УникальныйИдентификатор);
            
            ...
        ```

!!! Например

    === "Неправильно"

        ```bsl title="Устанавливать отбор подобный условию: дата не больше переданной в форму" hl_lines="5-6"
        &НаКлиенте
        Процедура ЗачетАвансовДокументАвансаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
            
            ПараметрыФормы = Новый Структура;
            ПараметрыФормы.Вставить("КонецПериода",
                ?(ЗначениеЗаполнено(Параметры.Ключ), Объект.Дата - 1, КонецДня(ТекущаяДата()))); // вызов ТекущаяДата() на клиенте
            ...
            ОткрытьФорму("Документ.ДокументРасчетовСКонтрагентом.ФормаВыбора", ПараметрыФормы, Элемент);
            ...
        ```
    === "Правильно"

        ```bsl title="Вычислять отбор по дате документа" hl_lines="5-6"
        &НаКлиенте
        Процедура ЗачетАвансовДокументАвансаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
            
            ПараметрыФормы = Новый Структура;
            ПараметрыФормы.Вставить("КонецПериода",
                ?(ЗначениеЗаполнено(Параметры.Ключ), Объект.Дата - 1, КонецДня(Объект.Дата)));
            ...
            ОткрытьФорму("Документ.ДокументРасчетовСКонтрагентом.ФормаВыбора", ПараметрыФормы, Элемент);
            ...
        ```

###### 3.4.

При использовании БСП используйте `#!bsl ОбщегоНазначенияКлиент.ДатаСеанса` там где не смогли отказаться от расчета даты на клиенте.

###### 4.

Могут появиться исключения из правил описанных выше. Если это произошло оставьте в коде подробные комментарии зачем это сделано. Это поможет другим разработчикам читающим ваш код понять почему системы статического анализа выдают предупреждение на вашем коде.

###### 5.

Не вызывайте подряд несколько раз функции `#!bsl ТекущаяДатаСеанса` (`#!bsl ТекущаяДата`). Возвращаемые значения будут отличаться. Это может привести к ошибкам.

!!! Например

    === "Неправильно"

        ```bsl title="Рассчитывать дату каждый раз"
        ДатаПоследнегоОповещения = ТекущаяДатаСеанса();
        ДатаСледующегоОповещения = РассчитатьДату() + ТекущаяДатаСеанса();
        ```
    === "Правильно"

        ```bsl title="Использовать ранее рассчитанную дату"
        ДатаПоследнегоОповещения = ТекущаяДатаСеанса();
        ДатаСледующегоОповещения = РассчитатьДату() + ДатаПоследнегоОповещения;
        ```